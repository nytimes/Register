apply from: 'buildsystem/dependencies.gradle'

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }

        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }

        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.10'
    }

}

allprojects {
    repositories {
        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }

        mavenCentral()
        google()
    }

    // Workaround to prevent Gradle from stealing focus from other apps during tests run/etc.
    // https://gist.github.com/artem-zinnatullin/4c250e04636e25797165
    tasks.withType(JavaForkOptions) {
        jvmArgs '-Djava.awt.headless=true'
    }
}

ext {
    // SDK versions

    // POM file
    GROUP = "com.nytimes.android"
    VERSION_NAME = "0.0.3.1-SNAPSHOT"
    POM_PACKAGING = "pom"
    POM_DESCRIPTION = "register"

    POM_URL = "https://github.com/nytimes/register/"
    POM_SCM_URL = "https://github.com/nytimes/register/"
    POM_SCM_CONNECTION = "scm:git:https://github.com/nytm/register.git"
    POM_SCM_DEV_CONNECTION = "scm:git:git@github.com:nytm/register.git"

    POM_LICENCE_NAME = "Apache License"
    POM_LICENCE_URL = "http://www.apache.org/licenses/"
    POM_LICENCE_DIST = "repo"

    POM_DEVELOPER_ID = "nytimesandroid"
    POM_DEVELOPER_NAME = "New York Times"
}

// From command line use: -PdisablePreDex to disable it: primarily for jenkins
project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
    project.plugins.apply('net.ltgt.errorprone')

    configurations.errorprone {
        resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.0.15'
    }
}

task gitHooksInit (type:Exec) {
    workingDir "$projectDir"
    commandLine './init-git-hooks'
}
